plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id "io.freefair.lombok" version "5.1.0"
    id 'java'
}

group = pluginGroup
version = pluginVersion

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
    maven { url = 'https://jitpack.io' }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven {
        url = 'https://papermc.io/repo/repository/maven-public/'
        name = 'papermc'
    }
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

shadowJar {
    ///////////////////////////////////////////////////////////////////////////////////////////////////
    setArchiveName('SafeChat-' + this.version + '.jar')                                              //
    ///////////////////////////////////////////////////////////////////////////////////////////////////
    // Relocations to avoid conflicts at runtime:                                                     //
    relocate 'com.zaxxer.hikari', 'me.thevipershow.safechat.core.libs.com.zaxxer.hikari'             //
    relocate 'co.aikar.commands', 'me.thevipershow.safechat.core.libs.acf'                           //
    relocate 'co.aikar.locales', 'me.thevipershow.safechat.core.libs.locales'                        //
    relocate 'co.aikar.idb', 'me.thevipershow.safechat.core.libs.idb'                                //
    relocate 'org.checkerframework', 'me.thevipershow.safechat.core.libs.checkerframework'           //
    relocate 'net.kyori', 'me.thevipershow.safechat.core.libs.kyori'                                 //
    relocate 'org.slf4j', 'me.thevipershow.safechat.core.libs.slf4j'                                 //
    ///////////////////////////////////////////////////////////////////////////////////////////////////
}

dependencies {
    ///////////////////////////////////////////////////////////////////////////////////////////////////
    // Here below you can find all the dependencies regarding databases                               //
    // Drivers: [PostgreSQL]                                                                         //
    // Connection Pool: [HikariCP]                                                                   //
    compile('com.zaxxer:HikariCP:3.4.5')                                                             //
    ///////////////////////////////////////////////////////////////////////////////////////////////////
    // Minecraft: here below you can find all the imported dependencies regarding minecraft           //
    // By Aikar: [IDB, ACF, TaskChain]                                                               //
    // By kashike: [Text-Adapter-Bukkit]                                                             //
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.30'                              //
    compile 'co.aikar:acf-paper:0.5.0-SNAPSHOT'                                                      //
    compile 'co.aikar:idb-bukkit:1.0.0-SNAPSHOT'                                                     //
    compile 'net.kyori:text-api:3.0.3'                                                               //
    compile ("net.kyori:text-serializer-gson:3.0.3") {                                               //
        exclude module: 'gson'                                                                       //
    }                                                                                                //
    compile('net.kyori:text-adapter-bukkit:3.0.4-SNAPSHOT') {                                        //
        exclude module: 'gson'                                                                       //
        exclude module: 'text-api'                                                                   //
        exclude module: 'text-serializer-gson'                                                       //
    }                                                                                                //
    compileOnly group: 'com.destroystokyo.paper', name: 'paper-api', version: '1.15.2-R0.1-SNAPSHOT' //
    compile project(':SafeChat-Core')                                                                //
}

import org.apache.tools.ant.filters.ReplaceTokens

/**
 * This allows the plugin to find placeholders @version@
 * and replace it with the current plugin's version
 * especially useful for Bukkit's plugin.yaml
 */
processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

build.dependsOn shadowJar
